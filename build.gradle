plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '1.2.0'
}

sourceCompatibility = 1.8
version = '1.0-SNAPSHOT'


if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile("io.vertx:vertx-core:$vertxVersion")
    compile("io.vertx:vertx-mongo-service:$vertxVersion")
    compile("io.vertx:vertx-metrics:$vertxVersion")
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'java:com.xonnec.webserver.App'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
    dependencies {
        exclude(dependency('io.vertx:codegen'))
        exclude(dependency('junit:junit'))
        exclude(dependency('org.mvel:mvel2'))
        exclude(dependency('log4j:log4j'))
        exclude(dependency('org.slf4j:slf4j-api'))
    }
}